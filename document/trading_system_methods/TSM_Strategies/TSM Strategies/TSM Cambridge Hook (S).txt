{ TSM Cambridge Hook
	Copyright 2012, P.J.Kaufman. All rights reserved.
	Oops, this is doing the opposite...successfully
	
	Rules for an uptrend:
	1. Outside day reversal
	2. RSI > 60
	3. Volume increasing
	4. Open Interest increasing
	
	Exit options:
	1.	Hold to next open
	2.	Hold to next close
	3. Hold extra day if profitable (loss?)
}		
	
	inputs: 	period(50), RSIperiod(14), RSIthreshold(60), exitoption(1), usestops(true), useopint(true);
	vars:		trend(0), RSIt(0), reversaldown(false), reversalup(false), size(1);
	
	trend = average(close,period);
	
	RSIt = RSI(close,RSIperiod);
	
	reversaldown = high > high[1] and close < close[1];
	reversalup = low < low[1] and close > close[1];
	
	if trend > trend[1] and reversaldown and RSIt > RSIthreshold and 
					volume > volume[1] and (useopint = false or openint > openint[1]) then begin
			buy size contracts this bar on close;
			if exitoption = 1 then sell next bar on open;
			end
		else if trend < trend[1] and reversalup and RSIt < 100 - RSIthreshold and
					volume > volume[1] and (useopint = false or openint > openint[1]) then begin
			sell short size contracts this bar on close;
			if exitoption = 1 then buy to cover next bar on open;
		end;
			
{ exits }
	if exitoption = 2 then begin
		if marketposition > 0 then sell all contract this bar on close
			else if marketposition < 0 then buy to cover all contracts this bar on close;
		end;
		
	if exitoption = 3 and marketposition <> 0 then begin
		if openpositionprofit > 0 then begin
				if marketposition > 0 then sell all contracts this bar on close
					else if marketposition < 0 then buy to cover all contracts this bar on close;
				end
			else begin
				if barssinceentry >= 2 then begin
					if marketposition > 0 then sell all contracts this bar on close
						else if marketposition < 0 then buy to cover all contracts this bar on close;
					end;
			end;
		end;
			
			